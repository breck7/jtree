title Tutorial: create a new Tree Language using Grammar

# Key Concepts

* *Tree Notation* is a notation for human & machine friendly languages that are as simple as possible. Simplicity is measured by the count of the pieces: the fewer pieces to build a language the better. We call languages that use _Tree Notation_ *Tree Languages*.

* A _Tree Notation_ program is a string, a tree, and a spreadsheet all at the same time. You will often work treat your program as all 3, sometimes concurrently.

* *Grammar* is a _Tree Language_ for building other _Tree Languages_. This tutorial walks you through building a very simple language in _Grammar_.

* _Grammar_ has only two main concepts: *Line Parsers* and *Cell Parsers*. _Line Parsers_ take a line(s) and handle it according to the definitions you write in your Grammar program. _Line Parsers_ delegate the parsing of the individual words (aka cells) in these lines to _Cell Parsers_.

## Before you start:
[] Know where to get help. GitHub issues, Tree Notation subreddit, Google Groups, or email Breck.
 https://github.com/treenotation/jtree/issues GitHub issues
 https://reddit.com/r/treenotation Tree Notation subreddit
 link mailto:treenotation@googlegroups.com Google Groups
 link mailto:breck7@gmail.com email Breck
[] Take a look at some of the sample languages in the Tree Language Designer. You might be able to use these as a reference if you get stuck.
 https://jtree.treenotation.org/designer/ Tree Language Designer
[] Open the Tree Language Designer or use your own editor (note: only SublimeText4 currently has partial syntax highlighting for Grammar Languages).
 https://jtree.treenotation.org/designer/ Tree Language Designer
 https://sublimetext.com SublimeText4

# Step 1
[] Name your language. For now, it is recommended that your language name be lowercase, only letters between A-Z, and not a common reserved word like "while/if/true/etc". You are free to name it anything you want, but if you do we ask that you report any bugs you encounter.

# Step 2
[] If you are using the Tree Language Designer, clear the boxes to create a new language from scratch. If you are using your own editor, create a new file with the name `{yourLanguageNameGoesHere}.grammar`
 https://jtree.treenotation.org/designer/ Tree Language Designer

## Step 3
[] Create your first lineParser. This will be the root parser for your language. It should like this:
psuedoCode
 {yourLanguageNameGoesHere}Node
  root

## Step 4
[] Add a description line to your new lineParser. This should describe what the purpose of your language is:
psuedoCode
 {yourLanguageNameGoesHere}Node
  root
  description {thisIsALangageToHelpYouDoX}

## Step 5
[] Add a top level lineParser to your language. In this example, we'll make a simple language that allows to store your friend's birthdays. Let's add a "friend" lineParser. We add a line to the `friendNode` `cruxFromId`. Most Tree Languages are keyword based. Because the word `keyword` is currently a reserved language in many programming languages, we currently use the word `crux` to refer to the same concept. The line `cruxFromId` instructs the GrammarParser to extract the keyword from the idâ€”`friendNode`. Ideally this will be cleaned up in a future version of Grammar.
grammarCode
 birthdaysNode
  root
  description A language for storing your friend's birthdays.
 friendNode
  description Store information about a friend.
  cruxFromId

## Step 6
[] Now let's add an `inScope` line to the root lineParser so that the friend lineParser is in scope:
grammarCode
 birthdaysNode
  root
  description A language for storing your friend's birthdays.
  inScope friendNode
 friendNode
  description Store information about a friend.
  cruxFromId

## Step 7
[] Now the following is a valid program in your language:
code
 friend
 friend
 friend

## Step 8
[] Now let's add a `nameNode` under the `friendNode`'s scope. [Code already covered in this tutorial will be represented with "..."]
psuedoCode
 ...
 friendNode
  description Store information about a friend.
  cruxFromId
  inScope nameNode
 nameNode

## Step 9
[] Now the following is a valid program in your language:
psuedoCode
 friend
  name
 friend
  name
 ...

## Step 10
[] Now let's add a `CellParser` which let's us start getting things like type checking, syntax highlighting, and autocomplete. By the *current* convention, we put the CellParsers at the top of our grammar file.
psuedoCode
 nameCell
  highlightScope string
 ...

## Step 11
[] Now let's make `nameNode` accept a "catch all" cellParser of the `nameCell`.
psuedoCode
 ...
 nameNode
  catchAllCellType nameCell

## Step 12
[] Now the following is a valid program in your language:
code
 friend
  name Ben Franklin
 friend
  name Ada Lovelace

## Step 13
[] Now let's add an error lineParser and link it to the lineParser to catch errors:
grammarCode
 birthdaysNode
  root
  description A language for storing your friend's birthdays.
  inScope friendNode
  catchAllNode errorNode
 errorNode
  baseNode errorNode
[] Now you should get an error for a program like this:
code
 frieeeend
  name Ben Franklin

## Step 14
* That's all for now! Let us know what you need to do next. Of course, we haven't really gotten to the fun stuff yet. Languages get really interesting once you start implementing the "interesting methods", like compile, execute, render, and format.

copyButtons