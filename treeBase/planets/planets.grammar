anyCell
filenameCell
stringCell
boolCell
intCell
 highlightScope constant.numeric.integer
floatCell
 highlightScope constant.numeric.float
keywordCell
 highlightScope keyword
planetNode
 root
 string tableName planets
 todo Change pattern to postfix.
 pattern \.planet$
 cells filenameCell
 inScope abstractPropertyNode
 catchAllNodeType errorNode
errorNode
 baseNodeType errorNode
abstractPropertyNode
 abstract
 cruxFromId
 single
abstractStringPropertyNode
 cells keywordCell
 catchAllCellType stringCell
 abstract
 extends abstractPropertyNode
abstractBooleanPropertyNode
 cells keywordCell boolCell
 abstract
 extends abstractPropertyNode
abstractIntPropertyNode
 cells keywordCell intCell
 abstract
 extends abstractPropertyNode
titleNode
 extends abstractStringPropertyNode
descriptionNode
 extends abstractPropertyNode
 catchAllNodeType descriptionLineNode
 baseNodeType blobNode
descriptionLineNode
 catchAllCellType stringCell
akaNode
 extends abstractStringPropertyNode
 single false
surfaceGravityNode
 extends abstractIntPropertyNode
diameterNode
 extends abstractIntPropertyNode
moonsNode
 extends abstractIntPropertyNode
abstractFloatPropertyNode
 cells keywordCell floatCell
 abstract
 extends abstractPropertyNode
yearsToOrbitSunNode
 extends abstractFloatPropertyNode
hasLifeNode
 extends abstractBooleanPropertyNode
explanationNode
 baseNodeType blobNode
ageNode
 extends abstractIntPropertyNode
 contentKey value
 childrenKey description
 catchAllNodeType explanationNode
wikipediaPageViewsNode
 cells keywordCell intCell
 crux pageViews
 single
wikipediaNode
 extends abstractStringPropertyNode
 contentKey id
 inScope wikipediaPageViewsNode
neighborNode
 todo Needs to be unique
 cells stringCell intCell
 uniqueFirstWord
neighborsNode
 cells keywordCell
 extends abstractPropertyNode
 catchAllNodeType neighborNode